model problem3
uses "mmxprs", "mmodbc"; !gain access to the Xpress-Optimizer solver and read in a spreadsheet

declarations

    ! Constants
    N = 3    ! N different types of trucks
    Kmax = 7 ! Kmax max age of trucks
    L = 2    ! L is the Restoration constant
    S = 12   ! months in a year
    Y = 10	 ! Planning horizon

    ! Sets
    types  = 1..N    ! type  i
    years  = 1..Y   ! year  t
    months = 1..S   ! month s

    unrestorable_ages_I = 0..L
    unrestorable_ages_II = (Kmax + 2)..(Kmax + L + 1)
    restorable_ages  = (L + 1)..(Kmax + 1)
    
    salvagable_ages  = 1..(Kmax + 1)
    
    operational_ages = 0..(Kmax)
    
    all_ages = 0..(Kmax + L + 1)
    all_years = 0..Y
    restorable_years = (L + 1)..(Kmax + L + 1)
    
    ! Decision Variables
    n: array(types, years) of mpvar ! type i bought in year t 
    r: array(types, years, (0)..(Kmax + L + 1)) of mpvar ! type i & age k restored in year t 
    o: array(types, all_years, 0..(Kmax+1)) of mpvar ! type i & age k operating in year t
    s: array(types, years, salvagable_ages) of mpvar ! type i & age k salvaged in year t
	dm: array(months, years) of mpvar ! demand met in month s year t
    Sn: array(types, years) of mpvar
    Sm: array(types, restorable_years) of mpvar
    
    ! Parameters
    cn: array(types, years) of real ! Cost to buy type i at beginning of year t
    cr: array(types, restorable_ages)  of real ! Cost to restore type i at age k
    co: array(types, operational_ages)  of real ! Cost to operate type i at age k
    cs: array(types, salvagable_ages)  of real ! Salvage value of type i at age k
    
    demand:   array(months, years) of real ! Demand in month s year t
    p:		  array(months, years) of real ! revenue per demand satisfied
    capacity: array(types, operational_ages)   of real ! Capacity for type i age k

    limit_o: array(types) of real ! Max number operating each year
    limit_r: array(types) of real ! Max number restored each year

	Bn: array(types, years) of real ! bulk buying requirements
	Dn: array(types, years) of real ! buying discount amount
	
	Bm: array(types, restorable_years) of real ! bulk restoration requirements
	Dm: array(types, restorable_years) of real ! restoration discount amount	
    status:array({ XPRS_OPT,XPRS_UNF,XPRS_INF,XPRS_UNB }) of string

end-declarations

initializations from "mmodbc.odbc:UnitCostNewTrucks.xls"
   cn as "[B1:K4]"
end-initializations 

initializations from "mmodbc.odbc:UnitCostRestoration.xls"
   cr as "[B1:G4]"
end-initializations 

initializations from "mmodbc.odbc:MonthlyOperationalCost.xls"
   co as "[B1:I4]"
end-initializations 

initializations from "mmodbc.odbc:UnitSalvageValue.xls"
   cs as "[B1:I4]"
end-initializations 

initializations from "mmodbc.odbc:MonthlyAvailableBusiness.xls"
   demand as "[B1:K13]"
end-initializations 

initializations from "mmodbc.odbc:RevenueForDemandSatisfied.xls"
	p as "[B1:K13]"
end-initializations

initializations from "mmodbc.odbc:MonthlyTruckCapacity.xls"
   capacity as "[B1:I4]"
end-initializations 

initializations from "mmodbc.odbc:OperationalCapacity.xls"
   limit_o as "[B1:B4]"
end-initializations 

initializations from "mmodbc.odbc:AnnualRestorationCapacity.xls"
   limit_r as "[B1:B4]"
end-initializations 

initializations from "mmodbc.odbc:BulkDiscountData.xls"
	Bn as "[B2:K5]"
	Dn as "[B9:K12]"
	Bm as "[B16:I19]"
	Dm as "[B23:I26]"
end-initializations 

status::([XPRS_OPT,XPRS_UNF,XPRS_INF,XPRS_UNB])["Optimum found", "Unfinished","Infeasible","Unbounded"]

! Objective Function

total_profit := sum(m in months)(sum(t in years)(p(m, t) * dm(m,t))) - (sum(i in types)( sum(t in years)( cn(i,t) * n(i,t) + sum(ko in operational_ages)(12 * co(i,ko) * o(i,t,ko)) + sum(kr in restorable_ages)(cr(i,kr) * r(i,t,kr)) - sum(ks in salvagable_ages)(cs(i,ks) * s(i,t,ks))  ))) + (sum(i in types)( sum(t in years)(Sn(i, t) * Dn(i, t)))) + (sum(i in types)( sum(t in restorable_years)(Sm(i, t) * Dm(i, t))))    

! Contraints
forall(i in types, t in years)
maximum_operating_constraint(i,t):= sum(k in operational_ages)(o(i,t,k)) <= limit_o(i)

forall(i in types, t in years)
maximum_restoration_constraint(i,t):= sum(k in restorable_ages)(r(i,t,k)) <= limit_r(i)

forall(i in types, t in years, k in unrestorable_ages_I)
restoration_constraint_I(i,t,k):= r(i,t,k) = 0

forall(i in types, t in years, k in unrestorable_ages_II)
restoration_constraint_II(i,t,k):= r(i,t,k) = 0

forall(i in types, t in years, k in restorable_ages)
restoration_constraint_III(i, t, k):= r(i, t, k) <= o(i, t - 1, k - 1)

forall(i in types, t in years)
maximum_age_constraint_II(i, t):= o(i, t, Kmax + 1) = 0

forall(i in types, t in years)
maximum_age_constraint(i, t):=s(i,t, Kmax + 1) = o(i, t - 1, Kmax) - r(i,t,Kmax + 1)

forall(i in types, t in years, k in salvagable_ages)
maximum_salvage_constraint(i, t, k):=s(i, t, k) <= o(i, t - 1, k - 1) - r(i, t, k) + r(i, t, k + L)

forall(i in types, k in operational_ages)
initial_supply(i,k):= o(i,0,k) = 0

forall(i in types, t in years, k in 1..Kmax) ! this should be good
operation_constraint(i, t, k):=o(i, t, k) = o(i, t - 1, k - 1) - r(i, t, k) + r(i, t, k + L) - s(i, t, k)

forall(i in types, t in years) ! this should be good
new_truck_constraint(i, t):=o(i, t, 0) = n(i,t)

forall(t in years, m in months)
demand_constraint_I(t, m):= dm(m, t) <= demand(m, t)

forall(t in years, m in months) !this should be good
demand_constraint_II(t, m):= sum(k in operational_ages)(sum(i in types)(o(i, t, k) * capacity(i, k))) >= dm(m,t)

forall(i in types, t in years)
binary_ordering_constraint(i, t):= Bn(i, t)*Sn(i, t) <= n(i, t)

forall(i in types, t in restorable_years)
binary_restoration_constraint(i, t):=  Bm(i, t)*Sm(i, t) <= sum(k in restorable_ages)(r(i,t,k))

forall(i in types, t in years)
nonnegativity_n(i, t):= n(i, t) >= 0

forall(i in types, t in years, k in salvagable_ages)
nonnegativity_s(i, t, k):= s(i, t, k) >= 0

forall(i in types, t in years, k in 1..(Kmax+L+1))
nonnegativity_r(i, t, k):= r(i, t, k) >= 0

forall(i in types, t in all_years, k in operational_ages)
nonnegativity_o(i, t, k):= o(i, t, k) >= 0

forall(m in months, t in years)
nonnegativity_dm(m, t):= dm(m, t) >= 0

forall(i in types, t in years)
binary_Sn(i, t):= Sn(i, t) is_binary


forall(i in types, t in restorable_years)
binary_Sm(i, t):= Sm(i, t) is_binary





writeln("Begin running model")
!...
writeln("End running model")

maximize(total_profit)
writeln("Total profit: ", getobjval)

end-model
